apply plugin: 'java-platform'
apply from: "$rootDir/gradle/any/dependencies.gradle"

// Testing related dependencies used by the tds are defined here
// not published, used to keep tds parent bom "clean" (i.e. some things are set as api simply for testing)

javaPlatform {
  allowDependencies()
}

dependencies {
  api enforcedPlatform("edu.ucar:netcdf-java-testing-platform:${depVersion.netcdfJava}")
  api enforcedPlatform(project(':tds-platform'))

  constraints {

    // Spring
    // Spring-test v5.3.4 introduced a hard dependency on servlet api v4.
    // That will be rolled back in v5.4.5, so for now, we specifically ask for
    // spring-test 5.3.3.
    api 'org.springframework:spring-test:5.3.27'

    // opendap
    api 'org.apache.taglibs:taglibs-standard-spec'
    api 'org.apache.taglibs:taglibs-standard-impl'

    // threddsIso
    api 'EDS:tds-plugin:2.4.0-SNAPSHOT' // api because of :it
    api 'net.sf.saxon:Saxon-HE:9.7.0-8' // api because of :it

    // tds
    api "jaxen:jaxen:${depVersion.jaxen}"  // api because of XPath operations in mock tests

    // testing
    api 'com.google.truth:truth'
    api 'pl.pragmatists:JUnitParams'
    api 'commons-io:commons-io'
    api 'junit:junit'
    api 'org.hamcrest:hamcrest-core:2.2'

    // only needed for it subproject
    api 'org.apache.commons:commons-lang3:3.4' // replace?
    api 'org.xmlunit:xmlunit-core:2.7.0'  // For comparing catalog XML.

  }
}

publishing {
  publications {
    tdsTestingPlatformBom(MavenPublication) {
      from components.javaPlatform
    }
  }
}
