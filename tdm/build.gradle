import java.nio.file.Paths

ext.title = 'THREDDS Data Manager (TDM)'

apply from: "$rootDir/gradle/any/dependencies.gradle"

// Because of the shadow jar (tdmFat.jar), we won't use java-published.gradle
// for this subproject.
apply from: "$rootDir/gradle/any/java-published.gradle"

apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
  implementation enforcedPlatform(project(':tds-platform'))

  implementation project(':tdcommon')
  implementation 'edu.ucar:httpservices'
  implementation 'edu.ucar:cdm-core'
  implementation 'edu.ucar:grib'

  implementation 'org.apache.httpcomponents:httpclient'
  implementation 'org.jdom:jdom2'
  implementation 'org.springframework:spring-beans'
  implementation 'org.springframework:spring-core'
  implementation 'org.springframework:spring-context'
  implementation 'com.google.protobuf:protobuf-java'
  implementation 'com.beust:jcommander'
  implementation 'com.google.guava:guava'
  implementation 'com.google.re2j:re2j'

  implementation 'org.slf4j:slf4j-api'
  implementation 'org.apache.logging.log4j:log4j-slf4j-impl'
}

shadowJar {
  doFirst {
    manifest.attributes jar.manifest.attributes
  }
  manifest.attributes 'Main-Class': 'thredds.tdm.Tdm'
  mergeServiceFiles()
  append 'META-INF/spring.factories'
  append 'META-INF/spring.handlers'
  append 'META-INF/spring.schemas'
  append 'META-INF/spring.tooling'
  destinationDirectory = Paths.get(rootDir.absolutePath, "build", "downloads").toFile()
  archiveFileName = "tdm-${project.getVersion()}.jar"
}

assemble.dependsOn shadowJar
