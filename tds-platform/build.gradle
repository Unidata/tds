apply plugin: 'java-platform'
apply from: "$rootDir/gradle/any/dependencies.gradle"
apply from: "$rootDir/gradle/any/publishing.gradle"

// All dependencies used by the thredds-data-server project and subprojects are defined here

javaPlatform {
  allowDependencies()
}

dependencies {
  // enforcedPlatform says that we *must* use the versions defined by these platforms
  // when resolving dependencies (even transitively)
  // If there is a conflict (previously hidden by optimistic resolution), we should get an error when we build, and
  // at that point we can take things on a case-by-case basis.
  api enforcedPlatform("edu.ucar:netcdf-java-bom:${depVersion.netcdfJava}")
  api enforcedPlatform('org.springframework:spring-framework-bom:5.3.20')
  api enforcedPlatform('org.springframework.security:spring-security-bom:5.6.5')
  api platform('net.openhft:chronicle-bom:2.21ea74')
  api enforcedPlatform("org.apache.logging.log4j:log4j-bom:2.17.1")

  constraints {
    // dependencies without explicit versions are getting their version set by one of the platforms above
    api 'edu.ucar:cdm-core'
    api 'edu.ucar:httpservices'
    api 'edu.ucar:grib'

    // tds - listed as compile, but most are likely runtime
    api 'edu.ucar:bufr'
    api 'edu.ucar:cdm-radial'
    api 'edu.ucar:cdm-misc'
    api 'edu.ucar:cdm-image'
    api 'edu.ucar:cdm-s3'
    api 'edu.ucar:netcdf4'
    api 'edu.ucar:cdm-mcidas'
    api 'edu.ucar:waterml'

    api 'com.google.protobuf:protobuf-java'
    api 'net.java.dev.jna:jna'
    api 'com.google.guava:guava'
    api 'org.jdom:jdom2'
    api 'org.apache.httpcomponents:httpclient'
    api 'com.beust:jcommander'
    api 'joda-time:joda-time'

    api 'org.quartz-scheduler:quartz:2.3.2'
    api 'net.openhft:chronicle-map'

    // Annotations
    // Nullable
    api 'com.google.code.findbugs:jsr305'
    // Resource
    api 'javax.annotation:javax.annotation-api:1.3.2'

    // tds logging
    api 'org.slf4j:slf4j-api'
    api 'org.apache.logging.log4j:log4j-slf4j-impl'
    runtime 'ch.qos.logback:logback-classic'

    // Spring
    api 'org.springframework:spring-core'
    api 'org.springframework:spring-context'
    api 'org.springframework:spring-beans'
    api 'org.springframework:spring-web'
    api 'org.springframework:spring-webmvc'
    api 'org.springframework.security:spring-security-web'
    runtime 'org.springframework.security:spring-security-config'

    // opendap
    api 'edu.ucar:opendap'
    api "javax.servlet:javax.servlet-api:${depVersion.javaxServletApi}"
    runtime 'org.apache.taglibs:taglibs-standard-spec:1.2.5'
    runtime 'org.apache.taglibs:taglibs-standard-impl:1.2.5'

    // dap4
    api 'edu.ucar:d4core'
    api 'edu.ucar:d4lib'
    api 'edu.ucar:d4cdm'
    // defined in tds-testing-platform as well, but using api config
    runtime 'org.apache.logging.log4j:log4j-web'

    // reify
    api 'org.apache.httpcomponents:httpcore'

    // tds
    api 'org.json:json:20180813'
    api 'com.coverity.security:coverity-escapers:1.1.1'
    api 'org.thymeleaf:thymeleaf-spring5:3.0.15.RELEASE'
    api 'javax.validation:validation-api:2.0.1.Final'
    api "org.hibernate.validator:hibernate-validator:${depVersion.hibernateValidator}"
    api "org.hibernate.validator:hibernate-validator-annotation-processor:${depVersion.hibernateValidator}"
    api 'javax.el:javax.el-api:3.0.0'
    api 'org.glassfish:javax.el:3.0.0'

    // waterml stuff
    api 'org.apache.xmlbeans:xmlbeans'
    api 'org.n52.sensorweb:52n-xml-waterML-v20'
    api 'org.n52.sensorweb:52n-xml-om-v20'

    // edal-java (ncwms)
    def edalVersion = '1.5.0.4-SNAPSHOT'
    api "uk.ac.rdg.resc:edal-common:${edalVersion}"
    api "uk.ac.rdg.resc:edal-cdm:${edalVersion}"
    api "uk.ac.rdg.resc:edal-wms:${edalVersion}"
    api "uk.ac.rdg.resc:edal-graphics:${edalVersion}"
    api "uk.ac.rdg.resc:edal-godiva:${edalVersion}"

    // gwt version defined in gradle/any/shared-mvn-coords.gradle, accessible via gradle/any/dependencies.gradle
    api "com.google.gwt:gwt-dev:${depVersion.gwt}"
    api "com.google.gwt:gwt-user:${depVersion.gwt}"

    // threddsIso
    runtime 'EDS:tds-plugin:2.4.4-SNAPSHOT'
    runtime 'net.sf.saxon:Saxon-HE:9.7.0-8'
    runtime "jaxen:jaxen:${depVersion.jaxen}"
  }
}

publishing {
  publications {
    tdsPlatformBom(MavenPublication) {
      from components.javaPlatform
    }
  }
}
